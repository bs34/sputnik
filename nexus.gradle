apply plugin: 'com.bmuschko.nexus'

group = POM_GROUP

nexus {
    sign = false
    repositoryUrl = REPOSITORY_URL
    snapshotRepositoryUrl = SNAPSHOT_REPOSITORY_URL
}

project.uploadArchives {
    dependsOn project.check
    onlyIf {
        doUpload(project)
    }
}

private static boolean doUpload(final Project project) {
    if (project.name.endsWith("SNAPSHOT")) {
        return true
    }

    def aarName = project.name + '-' + project.version + '.aar'
    def jarName = project.name + '-' + project.version + '.jar'
    def repositoryPath = project.group.replace(".", "/") + "/" + project.name + "/" + project.version + "/"
    def aarPath = repositoryPath + aarName
    def jarPath = repositoryPath + jarName
    def repositoryUrl = "https://apps.tapadoo.com/nexus/content/repositories/tapadoo/"
    return !urlExists(repositoryUrl + aarPath) && !urlExists(repositoryUrl + jarPath)
}

// Check if there is already a project at the given URL
private static boolean urlExists(String repositoryUrl) {
    try {
        def connection = (HttpURLConnection) new URL(repositoryUrl).openConnection()
        def timeoutInMillis = 10000
        connection.setConnectTimeout(timeoutInMillis)
        connection.setReadTimeout(timeoutInMillis)
        connection.setRequestMethod("HEAD")
        def responseCode = connection.getResponseCode()
        // If response code is anything but 404 there is probably something there.
        // Return true just in-case.
        return responseCode != 404
    } catch (IOException ignored) {
        return false
    }
}